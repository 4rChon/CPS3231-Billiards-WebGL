<!DOCTYPE html>
<html>
  <head>
    <meta charset='utf-8'/>

    <!-- Vertex Shader script in GLSL -->
    <script id="vertex-shader" type="x-shader/x-vertex">
      // vertex attributes (input)
      attribute vec3 position;
      attribute vec3 normal;
      attribute vec3 color;
      attribute vec2 texcoords;

      // vertex shader output / fragment shader input
      varying vec3 v_color;
      varying vec3 v_normal;
      varying vec3 v_eye;
      varying vec3 v_light;
      varying vec3 v_light_axis;
      varying vec3 v_light_world;
      varying vec2 v_texcoords;

      // Camera - uniform variables (matrices)
      uniform mat4 view_matrix;
      uniform mat4 model_matrix;
      uniform mat4 projection_matrix;

      // Light - 4 uniform structs for storing light details
      struct Light_vertex_info 
      {
        vec3 position;
        vec3 direction;
        int type;
      };

      uniform Light_vertex_info light_vertex[4];

      void main(void) 
      {
        // Transform vertex position to vector in world space
        vec4 point = vec4(position, 1.0);

        // Compute model_view and model_view_projection matrices
        mat4 view_model_matrix = view_matrix * model_matrix;
        mat4 projection_view_model_matrix = projection_matrix * view_model_matrix;

        // Light Type : DIRECTIONAL
        if (light_vertex[0].type == 0)
        {
          v_light = -normalize((view_matrix * vec4(light_vertex[0].direction, 0.0)).xyz);       // light colour
        }

        // Light Type : POINT || SPOT
        else if (light_vertex[0].type == 1 || light_vertex[0].type == 2)
        {
          vec4 model_point_matrix = model_matrix * point;
          vec4 light_distance = vec4(light_vertex[0].position, 1.0) - model_point_matrix;

          v_light = normalize(view_matrix * light_distance).xyz;                              // light colour
          v_light_axis = normalize(view_matrix * vec4(light_vertex[0].direction, 0.0)).xyz;   // light axis
          v_light_world = light_distance.xyz;                                                 // light distance as position
        }

        // Surface normal (We're assuming no scaling)
        v_normal = normalize((view_model_matrix * vec4(normal, 0.0)).xyz);

        // Eye vector
        v_eye = -normalize(view_model_matrix * point).xyz;

        // Texture coordinates
        v_texcoords = texcoords;

        // Vertex color
        v_color = color;

        // Projected vertex position
        gl_Position = projection_view_model_matrix * point;
      }
    </script>

    <!-- Fragment Shader script in GLSL -->
    <script id="fragment-shader" type="x-shader/x-vertex">
      precision mediump float;

      // Passed from vertex shader
      varying vec3 v_color;
      varying vec3 v_normal;
      varying vec3 v_eye;
      varying vec3 v_light;
      varying vec3 v_light_axis;
      varying vec3 v_light_world;
      varying vec2 v_texcoords;

      // Material information
      uniform float u_shininess;
      uniform vec3 u_ambient;
      uniform vec3 u_diffuse;
      uniform vec3 u_specular;
      uniform sampler2D u_texture_0;

      // Light information structure
      struct Light_fragment_info 
      {
        int type;
        int attenuation;

        float alpha, beta;

        vec3 ambient;
        vec3 diffuse;
        vec3 specular;
      };

      uniform Light_fragment_info light_fragment[4];

      float INV_PI = 0.31830988618;
      float INV_2_PI = 0.15915494309;

      void main(void)
      {
        vec3 v_normalized_light = normalize(v_light);   // why not in vertex shader?
        float v_light_distance = length(v_light_world); // why not in vertex shader?

        // Compute attenuation
        float attenuation = 1.0;

        // Light Type : POINT || SPOT
        if (light_fragment[0].type == 1 || light_fragment[0].type == 2)
        {
          // Attenuation : LINEAR
          if (light_fragment[0].attenuation == 1)
          {
            attenuation = 1.0 / v_light_distance;
          }
          // Attenuation : QUAD
          else if (light_fragment[0].attenuation == 2)
          {
            attenuation = 1.0 / (v_light_distance * v_light_distance);
          }
        }

        // Light Type : SPOT
        // Compute further attenuation between inner and outer cone angles
        if (light_fragment[0].type == 2)
        {
          float cone_angle = dot(v_normalized_light, - normalize(v_light_axis));
          cone_angle = max(light_fragment[0].beta, min(light_fragment[0].alpha, cone_angle));
          cone_angle = (cone_angle - light_fragment[0].beta) / (light_fragment[0].alpha - light_fragment[0].beta);
          attenuation *= cone_angle;
        }

        // Compute reflection vector
        vec3 v_reflect = reflect(v_normalized_light, v_normal);

        // Reflectivity of components
        vec3 ambient = light_fragment[0].ambient * u_ambient;
        vec3 diffuse = light_fragment[0].diffuse * u_diffuse;
        vec3 specular= light_fragment[0].specular * u_specular;
        vec3 rho = texture2D(u_texture_0, v_texcoords.xy).xyz;

        // Compute phong specular and lambert diffuse
        float phong_specular = pow(max(dot(v_reflect, -v_eye), 0.0), u_shininess) * (u_shininess + 2.0) * INV_2_PI;
        float lambert_diffuse = max(dot(v_normal, v_normalized_light), 0.0) * INV_PI;

        gl_FragColor = vec4(
          clamp(v_color + ((ambient + diffuse * lambert_diffuse + specular * phong_specular) * rho * attenuation), vec3(0,0,0), vec3(1,1,1)),
          1.0
        );
      }
    </script>

    <!-- Container for converted images -->
    <div id="image-collection" style="display:none"></div>

    <script type="text/javascript" src="src/systems/context.js"></script>
    <!-- Assets -->
    <script type="text/javascript" src="res/textures.js"></script>
    <script type="text/javascript" src="res/meshes.js"></script>

    <!-- LinAlg Library -->
    <script type="text/javascript" src="src/math/matrix.js"></script>
    
    <!-- Physics System -->
      <!-- Matter Library -->
      <script type="text/javascript" src="src/systems/physics/matter/matter.min.js"></script>
    <script type="text/javascript" src="src/systems/physics/physics.js"></script>

    <!-- Graphics System -->
      <!-- Woggle Library -->
      <script type="text/javascript" src="src/systems/graphics/woggle/light.js"></script>
      <script type="text/javascript" src="src/systems/graphics/woggle/material.js"></script>
      <script type="text/javascript" src="src/systems/graphics/woggle/model.js"></script>
      <script type="text/javascript" src="src/systems/graphics/woggle/scene.js"></script>
      <script type="text/javascript" src="src/systems/graphics/woggle/camera.js"></script>
    <script type="text/javascript" src="src/systems/graphics/graphics.js"></script>

    <!-- Input System -->
    <script type="text/javascript" src="src/systems/input/input.js"></script>

    <!-- Game System -->
    <script type="text/javascript" src="src/systems/game/game.js"></script>

    <!-- Helpers -->
    <script type="text/javascript" src="src/helpers/loader.js"></script>
    <script type="text/javascript" src="src/helpers/factory.js"></script>
    <script type="text/javascript" src="src/helpers/geometry.js"></script>

    <!-- Main Script -->
    <script type="text/javascript" src="src/script.js"></script>
  </head>
  <body style='margin:0px' onload='main()'>
    <canvas id='canvas-cg-lab'
            style='position: absolute; background-color: black;'>
    </canvas>
  </body>
</html>